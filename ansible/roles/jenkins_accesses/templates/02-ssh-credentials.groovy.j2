#!groovy

import com.cloudbees.plugins.credentials.*
import com.cloudbees.plugins.credentials.domains.*
import com.cloudbees.plugins.credentials.impl.*
import jenkins.model.*
import com.cloudbees.jenkins.plugins.sshcredentials.impl.*
import hudson.util.Secret

def keyPath = "/var/lib/jenkins/.ssh/{{ jenkins_ssh_key_name }}"
def keyFile = new File(keyPath)

println "=== Starting SSH key setup ==="
println "Key path: ${keyPath}"
println "Key exists: ${keyFile.exists()}"
println "Key readable: ${keyFile.canRead()}"

if (!keyFile.exists()) {
    println "SSH private key not found: ${keyPath}"
    return
}

println "Key file size: ${keyFile.length()} bytes"
def privateKey = keyFile.text
println "Key loaded successfully"

def credentialsId = "jenkins_ssh_key"
def jenkinsUser = "jenkins"

def store = Jenkins.instance.getExtensionList(
    "com.cloudbees.plugins.credentials.SystemCredentialsProvider"
)[0].getStore()

def existing = CredentialsProvider
    .lookupCredentials(
        BasicSSHUserPrivateKey.class,
        Jenkins.instance,
        null,
        null
    )
    .find { it.id == credentialsId }

if (existing) {
    println "SSH Credentials already exist."
    return
}

def creds = new BasicSSHUserPrivateKey(
    CredentialsScope.GLOBAL,
    credentialsId,
    jenkinsUser,
    new BasicSSHUserPrivateKey.DirectEntryPrivateKeySource(privateKey),
    "",
    "Auto-created Jenkins SSH key"
)

println "=== SSH key setup completed ==="
store.addCredentials(Domain.global(), creds)
println "SSH key added to Credentials."
